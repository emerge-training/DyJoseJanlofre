import jk.log
import jk.sqlite
import jk.mysql
import jk.sql
import jk.env
import jk.time

class:

model Table
{
    id as string
    name as string
    description as string
    timeStampAdded as long
    timeStampLastUpdated as long
}

const TABLENAME = "tbl"
pvar db as MySQLDatabase

func forContext(ctx as LoggingContext) static as this
{
    var cstr = EnvironmentVariable.get("DY_DATABASE")
    Log.debug(ctx, "Opening database connection: '" .. cstr .. "'")
    db = MySQLDatabase.forConnectionStringSync(ctx, cstr)
    if not db:
        Error.throw("failedToConnectToDatabase", cstr)
    var v = new this()
    v.setDb(db)
    return v
}

func updateTable(table as SQLTableInfo)
{
    if not table:
        Error.throw("nullTable","updateTable")
    if not db:
        Error.throw("nullDb", "updateTable")
    if not db.ensureTableExistsSync(table):
        Error.throw("failedToUpdateTable", table.getName())
}

func updateTables
{
    var tbl = SQLTableInfo.forName(TABLENAME)
    tbl.addStringKeyColumn("id")
    tbl.addStringColumn("name")
    tbl.addStringColumn("description")
    tbl.addLongColumn("timeStampAdded")
    tbl.addLongColumn("timeStampLastUpdated")
    updateTable(tbl)
}

func addValue(tbl as Table) as Table
{
    assert tbl
    tbl.setId("1")
    tbl.setTimeStampAddedValue(SystemClock.asUTCSeconds())
    assert db.executeSync(db.prepareInsertStatementSync(TABLENAME, tbl.toDynamicMap()))
    return tbl
}

func updateValue(id as string, tbl as Table) as bool
{
    assert tbl
    tbl.setTimeStampLastUpdatedValue(SystemClock.asUTCSeconds())
    var criteria = new Table()
    criteria.setId(id)
    return db.executeSync(db.prepareUpdateStatementSync(TABLENAME, criteria.toDynamicMap(), tbl.toDynamicMap()))
}

func deleteValue(id as string) as bool
{
    var criteria = new Table()
    criteria.setId(id)
    return db.executeSync(db.prepareDeleteStatementSync(TABLENAME, criteria.toDynamicMap()))
}

func getValues as DynamicMap
{
    var v = new vector<Table>
    var it = assert db.querySync(db.prepareQueryAllStatementSync(TABLENAME)):
        return null
    while it{
        var o = it.next()
        if not o:
            break
        var tbl = Table.forJsonObject(o)
        if not tbl:
            continue
        v += tbl
    }
    var data = new DynamicMap()
    data.setObject("records", v)
    return data
}

func close
{
    if db:
        db.closeSync()
    db = null
}
